/*
 * Exif Notes
 * Copyright (C) 2023  Tommi Hirvonen
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

import org.apache.tools.ant.taskdefs.condition.Os
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.dagger.hilt.android'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'

android {
    signingConfigs {
        config {
            def propsFile = rootProject.file('keystore.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                if (Os.isFamily(Os.FAMILY_MAC)) {
                    storeFile file(props['storeFileMac'])
                }
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    storeFile file(props['storeFileWin'])
                }
                storePassword = props['storePassword']
                keyAlias = props['keyAlias']
                keyPassword = props['keyPassword']
            }
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    defaultConfig {
        applicationId 'com.tommihirvonen.exifnotes'
        compileSdk 34
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 45
        versionName "1.20.1"
        // Enabling this allows us to use resources when PNGs are generated during build-time
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        def propsFile = rootProject.file('googlemapsapi.properties')
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                resValue "string", "google_maps_key", props['googleMapsKey']
            } else {
                resValue "string", "google_maps_key", ''
            }
        }
        debug {
            signingConfig signingConfigs.config
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                resValue "string", "google_maps_key", props['googleMapsKey']
            } else {
                resValue "string", "google_maps_key", ''
            }
        }
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }
    namespace 'com.tommihirvonen.exifnotes'

    tasks.withType(Test).configureEach {
        def propsFile = rootProject.file('googlemapsapi.properties')
        if (propsFile.exists()) {
            def props = new Properties()
            props.load(new FileInputStream(propsFile))
            environment 'google_maps_key', props['googleMapsKey']
        }
    }

    return void // Used to mute a warning about not all execution paths returning a value
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.android.gms:play-services-maps:18.2.0'
    implementation 'com.google.android.gms:play-services-location:21.0.1'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation "androidx.core:core-ktx:1.12.0"
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.2'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation 'androidx.databinding:databinding-runtime:8.2.0'
    implementation 'androidx.work:work-runtime-ktx:2.9.0'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigation_version"
    implementation "io.ktor:ktor-http:$rootProject.ktor_version"
    implementation "io.ktor:ktor-client-core:$rootProject.ktor_version"
    implementation "io.ktor:ktor-client-cio:$rootProject.ktor_version"
    implementation 'com.google.android.libraries.places:places:3.2.0'
    implementation "com.google.dagger:hilt-android:$rootProject.hilt_version"
    kapt "com.google.dagger:hilt-compiler:$rootProject.hilt_version"

    // For local unit tests
    testImplementation "junit:junit:4.13.2"

    // For instrumented testing
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}

repositories {
    mavenCentral()
}
